plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'
}

if (file('../enc.properties').exists()) {
  apply from: '../enc.properties'
}

android {
  defaultConfig {
    applicationId "ie.otormaigh.playground"
    versionCode 1
    versionName "1.0"

    archivesBaseName = "playground-$versionName"
  }

  signingConfigs {
    debug {
      storeFile file('../signing/debug.keystore')

      // NOTES#15/03/2021
      enableV3Signing true
      enableV4Signing true
    }

    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file('../signing/release.keystore')
        storePassword project.properties['store_password']
        keyAlias project.properties['key_alias']
        keyPassword project.properties['key_password']

        // NOTES#15/03/2021
        enableV3Signing true
        enableV4Signing true
      }
    }
  }

  flavorDimensions 'api'
  productFlavors {
    mock {
      dimension 'api'
    }
    live {
      dimension 'api'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
    }

    release {
      if (file('../signing/release.keystore').exists()) {
        signingConfig signingConfigs.release
      } else {
        signingConfig signingConfigs.debug
      }

      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = true
      }
    }
  }

  hilt {
    enableAggregatingTask = false // NOTES#06/02/2022
  }
}

dependencies {
  implementation project(':feature:photos')
  implementation project(':feature:splash-screen')

  implementation deps.androidx.appcompat
  implementation deps.androidx.core_ktx
  implementation deps.androidx.navigation.fragment
  implementation deps.androidx.navigation.ui
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

  implementation deps.google.material

  implementation deps.google.dagger.hilt.android
  kapt deps.google.dagger.hilt.compiler

  implementation deps.timber
}